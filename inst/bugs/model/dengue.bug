model{
    for (j in 1 : N) {
       # Likelihood
       Y[j]  ~ dpois(mu[j])
       log(mu[j]) <- log(E[j]) + Int + inprod(beta[],X[j,]) + u[j] + v[j]

       # random noise
       v[j] ~ dnorm(0,tau.v)
       # Area-specific relative risk (for maps)
       RR[j] <- exp(Int + inprod(beta[],X[j,]) + u[j] + v[j])
    }

  # CAR prior distribution for random effects:
  u[1:N] ~ car.normal(adj[], weights[], num[], tau.u)
  
  for(k in 1:sumNumNeigh) {
     weights[k] <- 1
  }

  # Other priors:
  tau.u ~ dgamma(0.5, 0.0005)				# prior on precision
  tau.v ~ dgamma(0.5, 0.0005)				# prior on precision

  Int ~ dflat()

  for(i in 1 : P){
    beta[i] ~ dnorm(0,0.001)
  }
}

